PREFIX schema: <http://schema.org/>
PREFIX lvont: <http://lexvo.org/ontology#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
  ?s ?p ?o .
  ?s schema:inLanguage ?languageCode .
  ?s schema:about ?subject, ?ysoSubject .
} WHERE {
  {
    ?s ?p ?o .
    FILTER(?p NOT IN (schema:inLanguage, schema:about))
  }
  UNION
  {
    ?s schema:inLanguage ?languageVal .
    OPTIONAL {
      GRAPH ?lexvoGraph {
        ?langent lvont:iso6392BCode ?languageVal .
        ?langent lvont:iso639P1Code ?iso6391code .
      }
    }
    BIND(COALESCE(?iso6391code, ?languageVal) AS ?languageCode)
  }
  UNION
  {
    ?s schema:about ?topicLabel .
    BIND(STRLANG(REPLACE(?topicLabel, '--', ' -- '), 'fi') AS ?topicLabelFi)
    OPTIONAL {
      GRAPH ?ysaGraph { 
        ?ysac skos:prefLabel ?topicLabelFi .
        OPTIONAL { ?ysac skos:closeMatch ?ysoSubject }
        # Make sure it's really a YSA concept and not e.g. RDA Carrier type
        FILTER(STRSTARTS(STR(?ysac),'http://www.yso.fi/onto/ysa/'))
      }
    }
    OPTIONAL {
      GRAPH ?ysaGraph { 
        ?ysac skos:altLabel ?topicLabelFi .
        OPTIONAL { ?ysac skos:closeMatch ?ysoSubject }
        # Make sure it's really a YSA concept and not e.g. RDA Carrier type
        FILTER(STRSTARTS(STR(?ysac),'http://www.yso.fi/onto/ysa/'))
      }
    }
    BIND(IF(BOUND(?ysac), ?ysac, ?topicLabelFi) AS ?subject)
  }
}
