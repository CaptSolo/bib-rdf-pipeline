PREFIX schema: <http://schema.org/>
PREFIX lvont: <http://lexvo.org/ontology#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdau: <http://rdaregistry.info/Elements/u/>

CONSTRUCT {
  ?s ?p ?o .
  ?s schema:inLanguage ?languageCode .
  ?s schema:about ?subjectUri, ?subjectString, ?ysoSubject .
  ?s rdau:P60048 ?rdaCarrierType .
  ?s rdau:P60049 ?rdaContentType .
  ?s rdau:P60050 ?rdaMediaType .
  ?s ?p ?organization .
  ?organization schema:name ?organizationName .
  ?organization a schema:Organization .
} WHERE {
  {
    ?s ?p ?o .
    FILTER(?p NOT IN (schema:inLanguage, schema:about, rdau:P60048, rdau:P60049, rdau:P60050))
    FILTER NOT EXISTS { ?o a schema:Organization }
    FILTER NOT EXISTS { ?s a schema:Organization }
  }
  UNION
  {
    ?s schema:inLanguage ?languageVal .
    OPTIONAL {
      GRAPH ?lexvoGraph {
        ?langent lvont:iso6392BCode ?languageVal .
        ?langent lvont:iso639P1Code ?iso6391code .
      }
    }
    BIND(COALESCE(?iso6391code, ?languageVal) AS ?languageCode)
  }
  UNION
  {
    ?s schema:about ?subjectUri .
    FILTER(isIRI(?subjectUri))
  }
  UNION
  {
    ?s schema:about ?topicLabel .
    FILTER(isLiteral(?topicLabel))
    BIND(STRLANG(REPLACE(?topicLabel, '--', ' -- '), 'fi') AS ?topicLabelFi)
    OPTIONAL {
      GRAPH ?ysaGraph { 
        ?ysac skos:prefLabel ?topicLabelFi .
        OPTIONAL { ?ysac skos:exactMatch|skos:closeMatch ?ysoSubject }
        # Make sure it's really a YSA concept and not e.g. RDA Carrier type
        FILTER(STRSTARTS(STR(?ysac),'http://www.yso.fi/onto/ysa/'))
        # Make sure the YSO concept is really from YSO and not e.g. Allärs
        FILTER(STRSTARTS(STR(?ysoSubject),'http://www.yso.fi/onto/yso/'))
      }
    }
    OPTIONAL {
      GRAPH ?ysaGraph { 
        ?ysac skos:altLabel ?topicLabelFi .
        OPTIONAL { ?ysac skos:exactMatch|skos:closeMatch ?ysoSubject }
        # Make sure it's really a YSA concept and not e.g. RDA Carrier type
        FILTER(STRSTARTS(STR(?ysac),'http://www.yso.fi/onto/ysa/'))
        # Make sure the YSO concept is really from YSO and not e.g. Allärs
        FILTER(STRSTARTS(STR(?ysoSubject),'http://www.yso.fi/onto/yso/'))
      }
    }
    BIND(IF(!BOUND(?ysac), ?topicLabelFi, ?unbound) AS ?subjectString)
  }
  UNION
  {
    ?s rdau:P60048 ?carrierCategoryValue .
    BIND(STRLANG(?carrierCategoryValue, 'fi') AS ?carrierCategoryValueFi)
    OPTIONAL {
      GRAPH ?rdaCarrierGraph {
        ?rdaCarrierType skos:prefLabel ?carrierCategoryValueFi .
        # make sure it's a genuine RDA Carrier concept, not e.g. YSA concept
        ?rdaCarrierType skos:inScheme <http://rdaregistry.info/termList/RDACarrierType> .
      }
    }
  }
  UNION
  {
    ?s rdau:P60049 ?contentCategoryValue .
    BIND(STRLANG(?contentCategoryValue, 'fi') AS ?contentCategoryValueFi)
    OPTIONAL {
      GRAPH ?rdaContentGraph {
        ?rdaContentType skos:prefLabel ?contentCategoryValueFi .
        # make sure it's a genuine RDA Content concept, not e.g. YSA concept
        ?rdaContentType skos:inScheme <http://rdaregistry.info/termList/RDAContentType> .
      }
    }
  }
  UNION
  {
    ?s rdau:P60050 ?mediaCategoryValue .
    BIND(STRLANG(?mediaCategoryValue, 'fi') AS ?mediaCategoryValueFi)
    OPTIONAL {
      GRAPH ?rdaMediaGraph {
        ?rdaMediaType skos:prefLabel ?mediaCategoryValueFi .
        # make sure it's a genuine RDA Media concept, not e.g. YSA concept
        ?rdaMediaType skos:inScheme <http://rdaregistry.info/termList/RDAMediaType> .
      }
    }

  }
  UNION
  {
    ?s ?p ?org .
    ?org a schema:Organization .
    ?org schema:name ?orgName .
    OPTIONAL {
      GRAPH ?cnGraph {
        ?cnOrganization skos:prefLabel ?orgName .
      }
    }
    OPTIONAL {
      GRAPH ?cnGraph {
        ?cnOrganization skos:altLabel ?orgName .
        ?cnOrganization skos:prefLabel ?orgPrefLabel .
      }
    }
    BIND(COALESCE(?cnOrganization,?org) AS ?organization)
    BIND(COALESCE(?orgPrefLabel, ?orgName) as ?organizationName)
  }
}
