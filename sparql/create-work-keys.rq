PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX bflc: <http://id.loc.gov/ontologies/bflc/>
PREFIX madsrdf: <http://www.loc.gov/mads/rdf/v1#>
PREFIX dct: <http://purl.org/dc/terms/>

CONSTRUCT {
  ?w dct:identifier ?strkey .
  ?w dct:identifier ?strextrakey .
} WHERE {
  # main case, where we have both author and title
  {
    ?w a bf:Work .

    OPTIONAL {
      ?w bf:title/bf:mainTitle ?title .
      BIND(?title AS ?fulltitle)
      
      # also try to add the translated title as an extra key, for the benefit of other
      # expression records which lack 240 information about the original work
      OPTIONAL {
        ?inst bf:instanceOf ?w .
        ?inst bf:titleStatement ?translatedTitle .
      }
    }
    # otherwise fall back on workTitle
    OPTIONAL {
      ?w bf:workTitle ?title .
      ?title bf:titleValue ?titleValue .
    FILTER NOT EXISTS { ?title bf:titleType "parallel" }
      OPTIONAL { ?title bf:subtitle ?subtitle }
      OPTIONAL { ?title bf:partNumber ?partNumber }
      OPTIONAL { ?title bf:partTitle ?partTitle }
      BIND(CONCAT(?titleValue, COALESCE(?subtitle, ''), COALESCE(?partNumber, ''), COALESCE(?partTitle, '')) AS ?fulltitle)
    }
    
    ?w bf:contribution ?contribution .
    ?contribution a bflc:PrimaryContribution .
    ?contribution bf:agent/rdfs:label ?creator .
    BIND(REPLACE(LCASE(?creator), '\\.|,? \\d+-\\d*', '') AS ?creatorkey)
    BIND(CONCAT(REPLACE(LCASE(?fulltitle),'[^\\p{L}\\p{N}\\s]',''), '/', ?creatorkey) AS ?key)
    BIND(CONCAT(REPLACE(LCASE(?translatedTitle),'[^\\p{L}\\p{N}\\s]',''), '/', ?creatorkey) AS ?extrakey)
  }
  UNION
  # uniform title case (130)
  {
    ?w a bf:Work .
    ?w bf:hasInstance [] . # not a series
    ?w bf:title/bf:mainTitle ?title .
    FILTER NOT EXISTS {
       # has no primary contributor
       ?w bf:contribution ?contribution . 
       ?contribution a bflc:PrimaryContribution .
    }
    BIND(REPLACE(LCASE(?title),'[^\\p{L}\\p{N}\\s]','') AS ?key)
  }
  UNION
  # has title (245) but no uniform title or primary authors (1xx)
  {
    ?w a bf:Work .
    FILTER NOT EXISTS { ?w bf:creator [] }
    FILTER NOT EXISTS { ?w madsrdf:authoritativeLabel [] }
    ?w bf:workTitle ?title .
    ?title bf:titleValue ?titleValue .
    FILTER NOT EXISTS { ?title bf:titleType "parallel" }
    OPTIONAL { ?title bf:subtitle ?subtitle }
    OPTIONAL { ?title bf:partNumber ?partNumber }
    OPTIONAL { ?title bf:partTitle ?partTitle }
    BIND(CONCAT(?titleValue, COALESCE(?subtitle, ''), COALESCE(?partNumber, ''), COALESCE(?partTitle, '')) AS ?fulltitle)
    ?w ?role ?contributor .
    { ?contributor a bf:Person } UNION { ?contributor a bf:Organization }
    FILTER (?role != bf:subject)
    ?contributor bf:label ?name .
    BIND(REPLACE(LCASE(?name), '\\.|,? \\d+-\\d*', '') AS ?namekey)
    BIND(CONCAT(REPLACE(LCASE(?fulltitle),'[^\\p{L}\\p{N}\\s]',''), '/', ?namekey) AS ?key)
  }
  # has only title (245) but no author (1xx or 7xx) - not relevant since records with no key are simply retained

  # strip recurring and whitespace from keys, as well as trailing space in the title part
  BIND(REPLACE(REPLACE(?key, '\\p{Z}+', ' '), ' +/', '/') AS ?strkey)
  BIND(REPLACE(REPLACE(?extrakey, '\\p{Z}+', ' '), ' +/', '/') AS ?strextrakey)
}
