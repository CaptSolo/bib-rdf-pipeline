PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX bflc: <http://id.loc.gov/ontologies/bflc/>
PREFIX madsrdf: <http://www.loc.gov/mads/rdf/v1#>
PREFIX dct: <http://purl.org/dc/terms/>

CONSTRUCT {
  ?w dct:identifier ?strkey .
  ?w dct:identifier ?strextrakey .
} WHERE {
  # main case, where we have both creator/contributor and title
  {
    ?w a bf:Work .

    ?w bf:title/rdfs:label ?title .
    BIND(?title AS ?fulltitle)
    
    # also try to add the translated title as an extra key, for the benefit of other
    # expression records which lack 240 information about the original work
    OPTIONAL {
      ?inst bf:instanceOf ?w .
      ?inst rdfs:label ?translatedTitle .
    }

    OPTIONAL {
      # case where we have a primary contibutor
      ?w ^bf:translationOf?/bf:contribution ?contribution .
      ?contribution a bflc:PrimaryContribution .
      ?contribution bf:agent/rdfs:label ?creator .
    }

    OPTIONAL {
      # fallback case where we don't have a primary contributor
      ?w ^bf:translationOf?/bf:contribution ?contribution .
      ?contribution bf:agent/rdfs:label ?creator .
    }

    OPTIONAL {
      # for translated works, try to guess the potential translator, based on contributors
      { ?w bf:note/rdfs:label "Includes translation" } UNION { ?w bf:translationOf [] }
      ?w bf:contribution ?translation .
      ?translation bf:agent ?translationAgent .
      ?translationAgent a bf:Person .
      ?translationAgent rdfs:label ?contributor .
      FILTER NOT EXISTS { ?translation a bflc:PrimaryContribution }
      BIND(COALESCE(?contributor, '-') AS ?translator)
    }

    BIND(REPLACE(LCASE(?creator), '(\\.|,)?( \\d+-\\d*)?(\\.|,?)$', '') AS ?creatorkey)
    BIND(REPLACE(LCASE(?translator), '(\\.|,)?( \\d+-\\d*)?(\\.|,?)$', '') AS ?translatorkey)
    BIND(IF(BOUND(?translator), CONCAT(?creatorkey, '/', ?translatorkey), ?creatorkey) AS ?creatorskey)
    BIND(CONCAT(REPLACE(LCASE(?fulltitle),'[^\\p{L}\\p{N}\\s]',''), '/', ?creatorskey) AS ?key)
    BIND(CONCAT(REPLACE(LCASE(?translatedTitle),'[^\\p{L}\\p{N}\\s]',''), '/', ?creatorskey) AS ?extrakey)
  }
  UNION
  # uniform title case (130)
  {
    ?w a bf:Work .
    ?w bf:hasInstance [] . # not a series
    ?w bf:title/rdfs:label ?title .
    FILTER NOT EXISTS {
       # has no contributors
       ?w bf:contribution ?contribution . 
    }
    BIND(REPLACE(LCASE(?title),'[^\\p{L}\\p{N}\\s]','') AS ?key)
  }
  UNION
  # Series with title
  {
    ?w2 bf:hasSeries/bf:instanceOf ?w .
    ?w a bf:Work .
    ?w bf:title/rdfs:label ?title .
    BIND(REPLACE(LCASE(?title),'[^\\p{L}\\p{N}\\s]','') AS ?key)
  }
  UNION
  # Series with ISSN
  {
    ?w a bf:Work .
    ?w bf:identifiedBy ?identifier .
    ?identifier a bf:Issn .
    ?identifier rdf:value ?issn .
    BIND(CONCAT("issn:", ?issn) AS ?key)
  }

  # has only title (245) but no author (1xx or 7xx) - not relevant since records with no key are simply retained

  # strip recurring and whitespace from keys, as well as trailing space from the title part and full key
  BIND(REPLACE(REPLACE(REPLACE(?key, '\\p{Z}+', ' '), ' +/', '/'), ' +$', '') AS ?strkey)
  BIND(REPLACE(REPLACE(REPLACE(?extrakey, '\\p{Z}+', ' '), ' +/', '/'), ' +$', '') AS ?strextrakey)
}
