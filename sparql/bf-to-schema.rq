PREFIX bf: <http://bibframe.org/vocab/>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX lvont: <http://lexvo.org/ontology#>
PREFIX rdau: <http://rdaregistry.info/Elements/u/>

# TODO:
# - numberOfPages values should be integer
# - datePublished sometimes has garbage e.g. copyright symbols and brackets e.g. "[1984]"
# - not everything should be typed schema:Book
# - add more detailed series types (e.g. BookSeries) when possible
# - classification codes should be added
# - publication events could be added
# - need to decide how to handle authority linking (people, organizations, places)

CONSTRUCT {
  ?inst a schema:Book, schema:CreativeWork ;
    schema:exampleOfWork ?work ;
    schema:name ?instName ;
    schema:description ?note ;
    schema:author ?creator ;
    schema:exampleOfWork ?work ;
    schema:isbn ?isbn10val, ?isbn13val ;
    schema:datePublished ?providerDate ;
    schema:numberOfPages ?extent ;
    rdau:P60048 ?rdaCarrierType ;
    rdau:P60050 ?rdaMediaType ;
    schema:publisher ?publisherOrganization .
  ?publisherOrganization a schema:Organization ;
    schema:name ?providerOrganizationLabel .
  ?creator a schema:Person ;
    schema:name ?creatorLabel .
  ?contributor a schema:Person ;
    schema:name ?contributorLabel .
  ?elec a schema:Book, schema:CreativeWork ;
    schema:exampleOfWork ?work ;
    schema:author ?creator ;
    schema:url ?elecUri ;
    schema:name ?instName ;
    schema:bookFormat schema:EBook .
  ?work a schema:CreativeWork ;
    schema:workExample ?inst ;
    schema:translationOfWork ?origwork ;
    schema:isPartOf ?series ;
    schema:author ?creator ;
    schema:contributor ?contributor ;
    schema:inLanguage ?languageCode ;
    schema:about ?subject, ?ysoSubject ;
    rdau:P60049 ?rdaContentType ;
    schema:name ?instName .
  ?origwork a schema:CreativeWork ;
    schema:workTranslation ?work ;
    schema:name ?origworkTitle ;
    schema:inLanguage ?origLanguageCode ;
    schema:author ?origworkCreator .
  ?origworkCreator a schema:Person ;
    schema:name ?origworkCreatorLabel .
  ?series a schema:CreativeWorkSeries, ?seriesType ;
    schema:hasPart ?work ;
    schema:name ?seriesTitleValue ;
    schema:issn ?seriesIssnValue .
}
WHERE {
  ?inst bf:instanceOf ?work .

  # title from the literal title statement
  { ?inst bf:titleStatement ?instNameRaw }
  UNION
  # title from the title resources
  {
    ?inst bf:instanceTitle ?instTitle .
    ?instTitle bf:titleValue ?instTitleValue .
    OPTIONAL { ?instTitle bf:subtitle ?instSubtitle }
    BIND(COALESCE(CONCAT(?instTitleValue, ' ', REPLACE(?instSubtitle,'^ +', '')), ?instTitleValue) AS ?instTitleCombined)
    OPTIONAL { ?instTitle bf:partNumber ?instPartNumber }
    OPTIONAL { ?instTitle bf:partTitle ?instPartTitle }
    BIND(COALESCE(
      CONCAT(?instTitleCombined, ' : ', ?instPartNumber, ', ', ?instPartTitle),
      CONCAT(?instTitleCombined, ' : ', ?instPartNumber),
      CONCAT(?instTitleCombined, ' : ', ?instPartTitle),
      ?instTitleCombined
    ) AS ?instNameRaw)
  }
  BIND(REPLACE(?instNameRaw, '[ \\.]+$', '') AS ?instName)
  OPTIONAL {
    ?inst bf:isbn10 ?isbn10 .
    BIND(STRAFTER(STR(?isbn10), 'http://isbn.example.org/') AS ?isbn10val) 
  }
  OPTIONAL {
    ?inst bf:isbn13 ?isbn13 
      BIND(STRAFTER(STR(?isbn13), 'http://isbn.example.org/') AS ?isbn13val) 
  }
  OPTIONAL {
    ?inst bf:publication ?provider .
    OPTIONAL {
      ?provider bf:providerDate ?providerDate .
    }
    OPTIONAL {
      ?provider bf:providerName ?providerOrganization .
      ?providerOrganization bf:label ?providerOrganizationLabel .
      OPTIONAL {
        GRAPH ?cnGraph {
          ?cnOrganization skos:prefLabel ?providerOrganizationLabel .
        }
      }
      BIND(COALESCE(?cnOrganization,?providerOrganization) AS ?publisherOrganization)
    }
  }
  OPTIONAL {
    ?inst bf:extent ?extent .
  }
  OPTIONAL {
    ?inst bf:note ?note .
  }
  OPTIONAL {
    ?elec bf:instanceOf ?work .
    ?elec a bf:Electronic .
    ?elec bf:uri ?elecUri .
  }
  OPTIONAL {
    ?work bf:creator ?creator .
    ?creator bf:label ?creatorLabel .
  }
  OPTIONAL {
    ?work bf:contributor ?contributor .
    ?contributor bf:label ?contributorLabel .
  }
  OPTIONAL {
    ?work bf:translationOf ?origwork .
    ?origwork bf:title ?origworkTitle .
    ?origwork bf:creator ?origworkCreator .
    ?origworkCreator bf:label ?origworkCreatorLabel .
    FILTER(LANG(?origworkTitle) != 'x-bf-sort')
  }
  OPTIONAL {
    ?work bf:series ?series .
    ?series bf:title ?seriesTitle .
    BIND(REPLACE(?seriesTitle, ',[^,]+$', '') AS ?seriesTitleValue)
    OPTIONAL {
      ?series bf:issn ?seriesIssn .
      BIND(STRBEFORE(STRAFTER(STR(?seriesIssn), 'urn:issn:'),';') AS ?seriesIssnValue)
      BIND(schema:Periodical as ?seriesType)
    }
  }

  ?work bf:language ?language .
  FILTER (STRSTARTS(STR(?language), 'http://id.loc.gov/vocabulary/languages/'))
  BIND(STRAFTER(STR(?language), 'http://id.loc.gov/vocabulary/languages/') AS ?languageVal)
  OPTIONAL {
    GRAPH ?lexvoGraph {
      ?langent lvont:iso6392BCode ?languageVal .
      ?langent lvont:iso639P1Code ?iso6391code .
    }
  }
  BIND(COALESCE(?iso6391code, ?languageVal) AS ?languageCode)

  OPTIONAL {
    ?work bf:language ?origLanguage .
    ?origLanguage bf:resourcePart "original" .
    ?origLanguage bf:languageOfPartUri ?origLanguageUri .
    BIND(STRAFTER(STR(?origLanguageUri), 'http://id.loc.gov/vocabulary/languages/') as ?origLanguageVal)
    OPTIONAL {
      GRAPH ?lexvoGraph {
        ?origlangent lvont:iso6392BCode ?origLanguageVal .
        ?origlangent lvont:iso639P1Code ?origIso6391code .
      }
    }
    BIND(IF(BOUND(?origIso6391code), ?origIso6391code, ?origLanguageVal) AS ?origLanguageCode)
  }

  OPTIONAL {
    ?work bf:subject ?topic .
    ?topic bf:authorizedAccessPoint ?topicLabel .
    BIND(STRLANG(REPLACE(?topicLabel, '--', ' -- '), 'fi') AS ?topicLabelFi)
    OPTIONAL {
      GRAPH ?ysaGraph { 
        ?ysac skos:prefLabel ?topicLabelFi .
        OPTIONAL { ?ysac skos:closeMatch ?ysoSubject }
        # Make sure it's really a YSA concept and not e.g. RDA Carrier type
        FILTER(STRSTARTS(STR(?ysac),'http://www.yso.fi/onto/ysa/'))
      }
    }
    OPTIONAL {
      GRAPH ?ysaGraph { 
        ?ysac skos:altLabel ?topicLabelFi .
        OPTIONAL { ?ysac skos:closeMatch ?ysoSubject }
        # Make sure it's really a YSA concept and not e.g. RDA Carrier type
        FILTER(STRSTARTS(STR(?ysac),'http://www.yso.fi/onto/ysa/'))
      }
    }
    BIND(IF(BOUND(?ysac), ?ysac, ?topicLabelFi) AS ?subject)
  }

  OPTIONAL {
    ?inst bf:carrierCategory ?carrierCategory .
    ?carrierCategory bf:categoryValue ?carrierCategoryValue .
    BIND(STRLANG(?carrierCategoryValue, 'fi') AS ?carrierCategoryValueFi)
    OPTIONAL {
      GRAPH ?rdaCarrierGraph {
        ?rdaCarrierType skos:prefLabel ?carrierCategoryValueFi .
        # make sure it's a genuine RDA Carrier concept, not e.g. YSA concept
        ?rdaCarrierType skos:inScheme <http://rdaregistry.info/termList/RDACarrierType> .
      }
    }
  }

  OPTIONAL {
    ?inst bf:mediaCategory ?mediaCategory .
    ?mediaCategory bf:categoryValue ?mediaCategoryValue .
    BIND(STRLANG(?mediaCategoryValue, 'fi') AS ?mediaCategoryValueFi)
    OPTIONAL {
      GRAPH ?rdaMediaGraph {
        ?rdaMediaType skos:prefLabel ?mediaCategoryValueFi .
        # make sure it's a genuine RDA Media concept, not e.g. YSA concept
        ?rdaMediaType skos:inScheme <http://rdaregistry.info/termList/RDAMediaType> .
      }
    }
  }

  OPTIONAL {
    ?work bf:contentCategory ?contentCategory .
    ?contentCategory bf:categoryValue ?contentCategoryValue .
    BIND(STRLANG(?contentCategoryValue, 'fi') AS ?contentCategoryValueFi)
    OPTIONAL {
      GRAPH ?rdaContentGraph {
        ?rdaContentType skos:prefLabel ?contentCategoryValueFi .
        # make sure it's a genuine RDA Content concept, not e.g. YSA concept
        ?rdaContentType skos:inScheme <http://rdaregistry.info/termList/RDAContentType> .
      }
    }
  }
}
