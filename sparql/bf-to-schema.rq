PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX bflc: <http://id.loc.gov/ontologies/bflc/>
PREFIX schema: <http://schema.org/>
PREFIX rdau: <http://rdaregistry.info/Elements/u/>

CONSTRUCT {
  ?inst a schema:Book, schema:CreativeWork ;
    schema:exampleOfWork ?work ;
    schema:name ?instName ;
    schema:description ?note ;
    schema:author ?creator ;
    schema:exampleOfWork ?work ;
    schema:isbn ?isbnVal ;
    schema:datePublished ?providerDate ;
    schema:numberOfPages ?extent ;
    rdau:P60048 ?carrierCategory ;
    rdau:P60050 ?mediaCategory ;
    schema:publisher ?providerOrganization .
  ?providerOrganization a schema:Organization ;
    schema:name ?providerOrganizationLabel .
  ?creator a ?schemaCreatorType ;
    schema:name ?creatorLabel .
  ?contributor a ?schemaContributorType ;
    schema:name ?contributorLabel .
  ?elec a schema:Book, schema:CreativeWork ;
    schema:exampleOfWork ?work ;
    schema:author ?creator ;
    schema:url ?elecUri ;
    schema:name ?instName ;
    schema:bookFormat schema:EBook .
  ?work a schema:CreativeWork ;
    schema:workExample ?inst ;
    schema:translationOfWork ?origwork ;
    schema:isPartOf ?series ;
    schema:author ?creator ;
    schema:contributor ?contributor ;
    schema:inLanguage ?languageVal ;
    schema:about ?topicLabel, ?subjectOrg ;
    rdau:P60049 ?contentCategory ;
    schema:name ?instName .
  ?origwork a schema:CreativeWork ;
    schema:workTranslation ?work ;
    schema:name ?origworkTitle ;
    schema:inLanguage ?origLanguageVal ;
    schema:author ?creator .
  ?series a schema:CreativeWorkSeries, ?seriesType ;
    schema:hasPart ?work ;
    schema:name ?seriesTitleValue ;
    schema:issn ?seriesIssnValue .
  ?subjectOrg a schema:Organization ;
    schema:name ?subjectOrgName .
}
WHERE {
  ?inst bf:instanceOf ?work .

  OPTIONAL {
    ?inst bf:title ?instTitle .
    ?instTitle bf:mainTitle ?instMainTitle .
    OPTIONAL { ?instTitle bf:subtitle ?instSubtitle }
    BIND(COALESCE(CONCAT(?instMainTitle, ' : ', ?instSubtitle), ?instMainTitle) AS ?instTitleCombined)
    OPTIONAL { ?instTitle bf:partNumber ?instPartNumber }
    OPTIONAL { ?instTitle bf:partName ?instPartName }
    BIND(COALESCE(
      CONCAT(?instTitleCombined, ' : ', ?instPartNumber, ', ', ?instPartName),
      CONCAT(?instTitleCombined, ' : ', ?instPartNumber),
      CONCAT(?instTitleCombined, ' : ', ?instPartName),
      ?instTitleCombined
    ) AS ?instNameRaw)
  }
#  OPTIONAL {
#    ?inst bf:title/rdfs:label ?instNameRaw .
#  }
  BIND(REPLACE(?instNameRaw, '[ \\./]+$', '') AS ?instName)

  OPTIONAL {
    ?inst bf:identifiedBy ?isbn .
    ?isbn a bf:Isbn .
    ?isbn rdf:value ?isbnVal .
  }
  OPTIONAL {
    ?inst bf:provisionActivity ?provision .
    OPTIONAL {
      ?provider bf:date ?providerDateRaw .
      BIND(STR(?providerDateRaw) AS ?providerDate)
    }
    OPTIONAL {
      ?provider bf:agent ?providerOrganization .
      ?providerOrganization rdfs:label ?providerOrganizationLabelRaw .
      BIND(REPLACE(?providerOrganizationLabelRaw, '^(jakelija:)? *(.*?)(,? jakaja)?[ \\.]*$', '$2') AS ?providerOrganizationLabel)
    }
  }
  OPTIONAL {
    ?inst bf:extent/rdfs:label ?extent .
  }
  OPTIONAL {
    ?inst bf:note/rdfs:label ?note .
  }
  OPTIONAL {
    ?elec bf:instanceOf ?work .
    ?elec a bf:Electronic .
    ?elec bf:uri ?elecUri .
  }
  OPTIONAL {
    VALUES (?bfCreatorType ?schemaCreatorType) { (bf:Person schema:Person) (bf:Jurisdiction schema:Organization) (bf:Organization schema:Organization)}
    ?work bf:contribution ?creation .
    ?creation a bflc:PrimaryContribution .
    ?creation bf:agent ?creator .
    ?creator a ?bfCreatorType .
    ?creator rdfs:label ?creatorLabelRaw .
    BIND(REPLACE(?creatorLabelRaw, '[ \\.]+$', '') AS ?creatorLabel)
  }
  OPTIONAL {
    VALUES (?bfContributorType ?schemaContributorType) { (bf:Person schema:Person) (bf:Jurisdiction schema:Organization) (bf:Organization schema:Organization)}
    ?work bf:contribution ?contribution .
    FILTER NOT EXISTS { ?contribution a bflc:PrimaryContribution }
    ?contribution bf:agent ?contributor .
    ?contributor a ?bfContributorType .
    ?contributor rdfs:label ?contributorLabelRaw .
    BIND(REPLACE(?contributorLabelRaw, '[ \\.]+$', '') AS ?contributorLabel)
  }
  OPTIONAL {
    ?work bf:translationOf ?origwork .
    ?origwork bf:title/rdfs:label ?origworkTitle .
  }
  OPTIONAL {
    ?work bf:series ?series .
    ?series bf:title ?seriesTitle .
    BIND(REPLACE(?seriesTitle, ',[^,]+$', '') AS ?seriesTitleValue)
    OPTIONAL {
      ?series bf:issn ?seriesIssn .
      BIND(STRBEFORE(STRAFTER(STR(?seriesIssn), 'urn:issn:'),';') AS ?seriesIssnValue)
      BIND(schema:Periodical as ?seriesType)
    }
  }

  ?work bf:language ?language .
  FILTER NOT EXISTS { ?language bf:part "original" }
  FILTER (STRSTARTS(STR(?language), 'http://id.loc.gov/vocabulary/languages/'))
  BIND(STRAFTER(STR(?language), 'http://id.loc.gov/vocabulary/languages/') AS ?languageVal)

  OPTIONAL {
    ?work bf:language ?origLanguage .
    ?origLanguage bf:part "original" .
    BIND(STRAFTER(STR(?origLanguage), 'http://id.loc.gov/vocabulary/languages/') as ?origLanguageVal)
  }

  OPTIONAL {
    # Topic as subject
    ?work bf:subject ?topic .
    ?topic a bf:Topic .
    ?topic rdfs:label ?topicLabel .
  }

  OPTIONAL {
    # Organization or Meeting as subject
    ?work bf:subject ?subjectOrg .
    ?subjectOrg rdfs:label ?subjectOrgNameRaw .
    BIND(REPLACE(?subjectOrgNameRaw, '[ \\.]+$', '') AS ?subjectOrgName)
    {
      { ?subjectOrg a bf:Organization } UNION { ?subjectOrg a bf:Meeting }
    }
  }

  OPTIONAL {
    ?inst bf:carrier/rdfs:label ?carrierCategory .
  }

  OPTIONAL {
    ?inst bf:media/rdfs:label ?mediaCategory .
  }

  OPTIONAL {
    ?work bf:content/rdfs:label ?contentCategory .
  }
}
