PREFIX bf: <http://bibframe.org/vocab/>
PREFIX schema: <http://schema.org/>
PREFIX rdau: <http://rdaregistry.info/Elements/u/>

CONSTRUCT {
  ?inst a schema:Book, schema:CreativeWork ;
    schema:exampleOfWork ?work ;
    schema:name ?instName ;
    schema:description ?note ;
    schema:author ?creator ;
    schema:exampleOfWork ?work ;
    schema:isbn ?isbn10val, ?isbn13val ;
    schema:datePublished ?providerDate ;
    schema:numberOfPages ?extent ;
    rdau:P60048 ?carrierCategoryValue ;
    rdau:P60050 ?mediaCategoryValue ;
    schema:publisher ?providerOrganization .
  ?providerOrganization a schema:Organization ;
    schema:name ?providerOrganizationLabel .
  ?creator a ?schemaCreatorType ;
    schema:name ?creatorLabel .
  ?contributor a ?schemaContributorType ;
    schema:name ?contributorLabel .
  ?elec a schema:Book, schema:CreativeWork ;
    schema:exampleOfWork ?work ;
    schema:author ?creator ;
    schema:url ?elecUri ;
    schema:name ?instName ;
    schema:bookFormat schema:EBook .
  ?work a schema:CreativeWork ;
    schema:workExample ?inst ;
    schema:translationOfWork ?origwork ;
    schema:isPartOf ?series ;
    schema:author ?creator ;
    schema:contributor ?contributor ;
    schema:inLanguage ?languageVal ;
    schema:about ?topicLabel, ?subjectOrg ;
    rdau:P60049 ?contentCategoryValue ;
    schema:name ?instName .
  ?origwork a schema:CreativeWork ;
    schema:workTranslation ?work ;
    schema:name ?origworkTitle ;
    schema:inLanguage ?origLanguageVal ;
    schema:author ?origworkCreator .
  ?origworkCreator a schema:Person ;
    schema:name ?origworkCreatorLabel .
  ?series a schema:CreativeWorkSeries, ?seriesType ;
    schema:hasPart ?work ;
    schema:name ?seriesTitleValue ;
    schema:issn ?seriesIssnValue .
  ?subjectOrg a schema:Organization ;
    schema:name ?subjectOrgName .
}
WHERE {
  ?inst bf:instanceOf ?work .

  # title from the literal title statement
  { ?inst bf:titleStatement ?instNameRaw }
  UNION
  # title from the title resources
  {
    ?inst bf:instanceTitle ?instTitle .
    ?instTitle bf:titleValue ?instTitleValue .
    OPTIONAL { ?instTitle bf:subtitle ?instSubtitle }
    BIND(COALESCE(CONCAT(?instTitleValue, ' ', REPLACE(?instSubtitle,'^ +', '')), ?instTitleValue) AS ?instTitleCombined)
    OPTIONAL { ?instTitle bf:partNumber ?instPartNumber }
    OPTIONAL { ?instTitle bf:partTitle ?instPartTitle }
    BIND(COALESCE(
      CONCAT(?instTitleCombined, ' : ', ?instPartNumber, ', ', ?instPartTitle),
      CONCAT(?instTitleCombined, ' : ', ?instPartNumber),
      CONCAT(?instTitleCombined, ' : ', ?instPartTitle),
      ?instTitleCombined
    ) AS ?instNameRaw)
  }
  BIND(REPLACE(?instNameRaw, '[ \\.]+$', '') AS ?instName)
  OPTIONAL {
    ?inst bf:isbn10 ?isbn10 .
    BIND(STRAFTER(STR(?isbn10), 'http://isbn.example.org/') AS ?isbn10val) 
  }
  OPTIONAL {
    ?inst bf:isbn13 ?isbn13 
      BIND(STRAFTER(STR(?isbn13), 'http://isbn.example.org/') AS ?isbn13val) 
  }
  OPTIONAL {
    ?inst bf:publication ?provider .
    OPTIONAL {
      ?provider bf:providerDate ?providerDate .
    }
    OPTIONAL {
      ?provider bf:providerName ?providerOrganization .
      ?providerOrganization bf:label ?providerOrganizationLabelRaw .
      BIND(REPLACE(?providerOrganizationLabelRaw, '^(jakelija:)? *(.*?)(,? jakaja)?[ \\.]*$', '$2') AS ?providerOrganizationLabel)
    }
  }
  OPTIONAL {
    ?inst bf:extent ?extent .
  }
  OPTIONAL {
    ?inst bf:note ?note .
  }
  OPTIONAL {
    ?elec bf:instanceOf ?work .
    ?elec a bf:Electronic .
    ?elec bf:uri ?elecUri .
  }
  OPTIONAL {
    VALUES (?bfCreatorType ?schemaCreatorType) { (bf:Person schema:Person) (bf:Jurisdiction schema:Organization) (bf:Organization schema:Organization)}
    ?work bf:creator ?creator .
    ?creator a ?bfCreatorType .
    ?creator bf:label ?creatorLabelRaw .
    BIND(REPLACE(?creatorLabelRaw, '[ \\.]+$', '') AS ?creatorLabel)
  }
  OPTIONAL {
    VALUES (?bfContributorType ?schemaContributorType) { (bf:Person schema:Person) (bf:Jurisdiction schema:Organization) (bf:Organization schema:Organization)}
    ?work bf:contributor ?contributor .
    ?contributor a ?bfContributorType .
    ?contributor bf:label ?contributorLabelRaw .
    BIND(REPLACE(?contributorLabelRaw, '[ \\.]+$', '') AS ?contributorLabel)
  }
  OPTIONAL {
    ?work bf:translationOf ?origwork .
    ?origwork bf:title ?origworkTitle .
    ?origwork bf:creator ?origworkCreator .
    ?origworkCreator bf:label ?origworkCreatorLabel .
    FILTER(LANG(?origworkTitle) != 'x-bf-sort')
  }
  OPTIONAL {
    ?work bf:series ?series .
    ?series bf:title ?seriesTitle .
    BIND(REPLACE(?seriesTitle, ',[^,]+$', '') AS ?seriesTitleValue)
    OPTIONAL {
      ?series bf:issn ?seriesIssn .
      BIND(STRBEFORE(STRAFTER(STR(?seriesIssn), 'urn:issn:'),';') AS ?seriesIssnValue)
      BIND(schema:Periodical as ?seriesType)
    }
  }

  ?work bf:language ?language .
  FILTER (STRSTARTS(STR(?language), 'http://id.loc.gov/vocabulary/languages/'))
  BIND(STRAFTER(STR(?language), 'http://id.loc.gov/vocabulary/languages/') AS ?languageVal)

  OPTIONAL {
    ?work bf:language ?origLanguage .
    ?origLanguage bf:resourcePart "original" .
    ?origLanguage bf:languageOfPartUri ?origLanguageUri .
    BIND(STRAFTER(STR(?origLanguageUri), 'http://id.loc.gov/vocabulary/languages/') as ?origLanguageVal)
  }

  OPTIONAL {
    # Topic as subject
    ?work bf:subject ?topic .
    ?topic a bf:Topic .
    ?topic bf:authorizedAccessPoint ?topicLabel .
  }

  OPTIONAL {
    # Organization or Meeting as subject
    ?work bf:subject ?subjectOrg .
    ?subjectOrg bf:authorizedAccessPoint ?subjectOrgNameRaw .
    BIND(REPLACE(?subjectOrgNameRaw, '[ \\.]+$', '') AS ?subjectOrgName)
    {
      { ?subjectOrg a bf:Organization } UNION { ?subjectOrg a bf:Meeting }
    }
  }

  OPTIONAL {
    ?inst bf:carrierCategory ?carrierCategory .
    ?carrierCategory bf:categoryValue ?carrierCategoryValue .
  }

  OPTIONAL {
    ?inst bf:mediaCategory ?mediaCategory .
    ?mediaCategory bf:categoryValue ?mediaCategoryValue .
  }

  OPTIONAL {
    ?work bf:contentCategory ?contentCategory .
    ?contentCategory bf:categoryValue ?contentCategoryValue .
  }
}
